COMPLETE_COMMAND -> LIST SEPARATOR
| LIST.
LIST             -> LIST SEPARATOR_OP AND_OR
|                   AND_OR.
AND_OR           ->                         PIPELINE
| AND_OR AND_IF LINEBREAK PIPELINE
| AND_OR OR_IF  LINEBREAK PIPELINE.
PIPELINE         ->      PIPE_SEQUENCE
| BANG PIPE_SEQUENCE.
PIPE_SEQUENCE    ->                             COMMAND
| PIPE_SEQUENCE '|' LINEBREAK COMMAND.
COMMAND          -> SIMPLE_COMMAND
| COMPOUND_COMMAND
| COMPOUND_COMMAND REDIRECT_LIST
| FUNCTION_DEFINITION.
COMPOUND_COMMAND -> BRACE_GROUP
| SUBSHELL
| FOR_CLAUSE
| CASE_CLAUSE
| IF_CLAUSE
| WHILE_CLAUSE
| UNTIL_CLAUSE.
SUBSHELL         -> '(' COMPOUND_LIST ')'.
COMPOUND_LIST    ->              TERM
| NEWLINE_LIST TERM
|              TERM SEPARATOR
| NEWLINE_LIST TERM SEPARATOR.
TERM             -> TERM SEPARATOR AND_OR
|                AND_OR.
FOR_CLAUSE       -> FOR NAME LINEBREAK                            DO_GROUP
| FOR NAME LINEBREAK IN          SEQUENTIAL_SEP DO_GROUP
| FOR NAME LINEBREAK IN WORDLIST SEQUENTIAL_SEP DO_GROUP.
NAME             -> NAME                     .
IN               -> IN                       .
WORDLIST         -> WORDLIST WORD
|          WORD.
CASE_CLAUSE      -> CASE WORD LINEBREAK IN LINEBREAK CASE_LIST    ESAC
| CASE WORD LINEBREAK IN LINEBREAK CASE_LIST_NS ESAC
| CASE WORD LINEBREAK IN LINEBREAK              ESAC.
CASE_LIST_NS     -> CASE_LIST CASE_ITEM_NS
|           CASE_ITEM_NS.
CASE_LIST        -> CASE_LIST CASE_ITEM
|           CASE_ITEM.
CASE_ITEM_NS     ->     PATTERN ')'               LINEBREAK
|     PATTERN ')' COMPOUND_LIST LINEBREAK
| '(' PATTERN ')'               LINEBREAK
| '(' PATTERN ')' COMPOUND_LIST LINEBREAK.
CASE_ITEM        ->     PATTERN ')' LINEBREAK     DSEMI LINEBREAK
|     PATTERN ')' COMPOUND_LIST DSEMI LINEBREAK
| '(' PATTERN ')' LINEBREAK     DSEMI LINEBREAK
| '(' PATTERN ')' COMPOUND_LIST DSEMI LINEBREAK.
PATTERN          ->             WORD         
| PATTERN '|' WORD         .
IF_CLAUSE        -> IF COMPOUND_LIST THEN COMPOUND_LIST ELSE_PART FI
| IF COMPOUND_LIST THEN COMPOUND_LIST           FI.
ELSE_PART        -> ELIF COMPOUND_LIST THEN ELSE_PART
| ELSE COMPOUND_LIST.
WHILE_CLAUSE     -> WHILE COMPOUND_LIST DO_GROUP.
UNTIL_CLAUSE     -> UNTIL COMPOUND_LIST DO_GROUP.
FUNCTION_DEFINITION -> FNAME '(' ')' LINEBREAK FUNCTION_BODY.
FUNCTION_BODY    -> COMPOUND_COMMAND                
| COMPOUND_COMMAND REDIRECT_LIST  .
FNAME            -> NAME                            .
BRACE_GROUP      -> LBRACE COMPOUND_LIST RBRACE.
DO_GROUP         -> DO COMPOUND_LIST DONE           .
SIMPLE_COMMAND   -> CMD_PREFIX CMD_WORD CMD_SUFFIX
| CMD_PREFIX CMD_WORD
| CMD_PREFIX
| CMD_NAME CMD_SUFFIX
| CMD_NAME.
CMD_NAME         -> WORD                   .
CMD_WORD         -> WORD                   .
CMD_PREFIX       ->            IO_REDIRECT
| CMD_PREFIX IO_REDIRECT
|            ASSIGNMENT_WORD
| CMD_PREFIX ASSIGNMENT_WORD.
CMD_SUFFIX       ->            IO_REDIRECT
| CMD_SUFFIX IO_REDIRECT
|            WORD
| CMD_SUFFIX WORD.
REDIRECT_LIST    ->               IO_REDIRECT
| REDIRECT_LIST IO_REDIRECT.
IO_REDIRECT      ->           IO_FILE
| IO_NUMBER IO_FILE
|           IO_HERE
| IO_NUMBER IO_HERE.
IO_FILE          -> '<'       FILENAME
| LESSAND   FILENAME
| '>'       FILENAME
| GREATAND  FILENAME
| DGREAT    FILENAME
| LESSGREAT FILENAME
| CLOBBER   FILENAME.
FILENAME         -> WORD                      .
IO_HERE          -> DLESS     HERE_END
| DLESSDASH HERE_END.
HERE_END         -> WORD                      .
NEWLINE_LIST     ->              NEWLINE
| NEWLINE_LIST NEWLINE.
LINEBREAK        -> NEWLINE_LIST
| .
SEPARATOR_OP     -> '&'
| ';'.
SEPARATOR        -> SEPARATOR_OP LINEBREAK
| NEWLINE_LIST.
SEQUENTIAL_SEP   -> ';' LINEBREAK
| NEWLINE_LIST.
